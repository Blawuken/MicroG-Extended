/*
 * SPDX-FileCopyrightText: 2013 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.applicationNamespace = "com.google.android.gms"
    ext.basePackageName = "com.mgoogle"

    ext.cronetVersion = '102.5005.125'
    ext.wearableVersion = '0.1.1'

    ext.kotlinVersion = '1.9.22'
    ext.coroutineVersion = '1.7.3'

    ext.annotationVersion = '1.7.1'
    ext.appcompatVersion = '1.6.1'
    ext.biometricVersion = '1.1.0'
    ext.coreVersion = '1.12.0'
    ext.fragmentVersion = '1.6.2'
    ext.lifecycleVersion = '2.7.0'
    ext.materialVersion = '1.11.0'
    ext.loaderVersion = '1.1.0'
    ext.mediarouterVersion = '1.6.0'
    ext.multidexVersion = '2.0.1'
    ext.navigationVersion = '2.7.7'
    ext.preferenceVersion = '1.2.0'
    ext.recyclerviewVersion = '1.3.2'
    ext.webkitVersion = '1.10.0'

    ext.slf4jVersion = '1.7.36'
    ext.volleyVersion = '1.2.1'
    ext.wireVersion = '4.9.7'

    ext.cardviewVersion = '1.0.0'
    ext.switchbuttonVersion = '2.1.0'
    ext.lottieVersion = '6.4.0'
    ext.skydoveselasticviewsVersion = '2.1.0'
    ext.okhttpVersion = '4.12.0'
    ext.jsoupVersion = '1.17.2'

    ext.androidBuildGradleVersion = '8.3.0'

    ext.androidBuildVersionTools = '34.0.0'

    ext.androidMinSdk = 27
    ext.androidTargetSdk = 30
    ext.androidCompileSdk = 34

    ext.localProperties = new Properties()
    
    ext.includeProjects = [
            ':play-services-base-core',
            ':play-services-cast-core',
            ':play-services-cast-framework-core',
            ':play-services-conscrypt-provider-core',
            ':play-services-core-proto', // deprecated
            ':play-services-core:microg-ui-tools',
            ':play-services-api',
            ':play-services-tasks-ktx',
            ':play-services-auth'
    ]

    try {
        var stream = rootProject.file('local.properties').newDataInputStream()
        ext.localProperties.load(stream)
        stream.close()
    } catch (ignored) {
        // Ignore
    }

    ext.hasModule = (String name, boolean enabledByDefault) -> {
        return ext.localProperties.getProperty("modules." + name, enabledByDefault.toString()).toBoolean()
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidBuildGradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.squareup.wire:wire-gradle-plugin:$wireVersion"
    }
}

def gmsVersion = "24.07.54"
def gmsVersionCode = Integer.parseInt(gmsVersion.replaceAll('\\.', ''))
def ourVersionCode = gmsVersionCode * 1000

allprojects {
    apply plugin: 'idea'

    group = 'org.microg.gms'
    ext.appVersionCode = ourVersionCode
    ext.isReleaseVersion = false
}

subprojects {
    repositories {
        mavenCentral()
        google()
    }
}
