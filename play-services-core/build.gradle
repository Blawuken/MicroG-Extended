/*
 * SPDX-FileCopyrightText: 2013 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

configurations {
    defaultRuntimeOnly
}

dependencies {
    implementation "com.squareup.wire:wire-runtime:$wireVersion"
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    includeProjects.each { projectName ->
        implementation project(projectName)
    }

    implementation project(':play-services-base-core-package')

    // AndroidX UI
    implementation "androidx.multidex:multidex:$multidexVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.mediarouter:mediarouter:$mediarouterVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.webkit:webkit:$webkitVersion"
    implementation "com.google.android.material:material:$materialVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "com.android.volley:volley:$volleyVersion"

    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Cardview
    implementation "androidx.cardview:cardview:1.0.0"
    
    // Switch Button
    implementation 'com.kyleduo.switchbutton:library:2.1.0'

    // Lottie
    implementation 'com.airbnb.android:lottie:6.3.0'

    // Anu
    implementation fileTree(dir: 'libs/', include: ['*.jar', '*.aar'])
}

android {
    namespace "$applicationNamespace"

    compileSdkVersion androidCompileSdk
    buildToolsVersion "$androidBuildVersionTools"

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "${basePackageName}.android.gms"

        versionName project.version as String
        versionCode appVersionCode

        minSdkVersion androidMinSdk
        targetSdkVersion androidTargetSdk

        multiDexEnabled true
        multiDexKeepProguard file('multidex-keep.pro')

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        buildConfigField "String", "APPLICATION_NAMESPACE", "\"${android.namespace}\""
        buildConfigField "String", "BASE_PACKAGE_NAME", "\"$basePackageName\""
        buildConfigField "String", "INCLUDED_PROJECTS", "\"${includeProjects.join(",")}\""
        buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
        
        manifestPlaceholders = [
                basePackageName: "$basePackageName"
        ]

        // provide package name to xml resources
        resValue "string", "var_authenticator_account_type", "$basePackageName"
        resValue "string", "var_contact_syncadapter_account_type", "$basePackageName"
        resValue "string", "var_pref_auth_action_security_settings", "${basePackageName}.android.gms.accountsettings.SECURITY_SETTINGS"
        resValue "string", "var_pref_auth_action_privacy_settings", "${basePackageName}.android.gms.accountsettings.PRIVACY_SETTINGS"
        resValue "string", "var_pref_auth_action_preferences_settings", "${basePackageName}.android.gms.accountsettings.ACCOUNT_PREFERENCES_SETTINGS"
        resValue "string", "var_pref_start_gms_package", "${basePackageName}.android.gms"
        resValue "string", "extended_build_date", "${getDate()}"
        resValue "string", "extended_build_min_sdk", "$androidMinSdk"
        resValue "string", "extended_build_target_sdk", "$androidTargetSdk"
        resValue "string", "extended_build_app_version_code", "$appVersionCode"
        resValue "string", "extended_build_app_version_release", "$version"
    }

    buildFeatures {
        dataBinding = true
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    lintOptions {
        disable 'MissingTranslation', 'GetLocales', 'InvalidPackage', 'BatteryLife', 'ImpliedQuantity', 'MissingQuantity', 'InvalidWakeLockTag', 'UniquePermission'
    }

    signingConfigs {
        defaultConfig {
            storeFile file('../platform.keystore')
            storePassword 'password'
            keyAlias 'platform'
            keyPassword 'password'

            // Optional, specify signing versions used
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.defaultConfig
        }
    }

    flavorDimensions = ['target']
    productFlavors {
        "default" {
            dimension 'target'
        }
        "huawei" {
            dimension 'target'
            versionNameSuffix "-hw"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = 1.8
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        jniLibs {
            useLegacyPackaging true
        }
    }
}

def getDate() {
    return new Date().format('dd, MMMM YYYY')
}

if (file('user.gradle').exists()) {
    apply from: 'user.gradle'
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.outputFileName = "MicroG-Extended" + "-" + variant.versionCode + variant.versionName.substring(version.length()) + ".apk"
    }
}
